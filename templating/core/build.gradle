// Copyright 2019 VMware, Inc.
// SPDX-License-Indentifier: Apache-2.0

repositories {
  mavenCentral()
  mavenLocal()
  jcenter()
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'signing'
apply plugin: 'maven'

dependencies {
  compile 'com.fasterxml.jackson.core:jackson-databind:2.9.7'
  compile 'org.python:jython:2.7.1b3'
  compile group: 'commons-cli', name: 'commons-cli', version: '1.3.1'
}

task javadocJar(type: Jar) {
  classifier = 'javadoc'
  from javadoc
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives javadocJar, sourcesJar
}

signing {
  sign configurations.archives
}

group = "com.vmware.jsonteng"
archivesBaseName = "jsonteng"
version = "1.0.3"

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'Json Template Engine'
        packaging 'jar'
        description 'A template engine for processing parameterized Json objects.'
        url 'https://github.com/vmware/json-template-engine'

        scm {
          connection 'scm:svn:https://github.com/vmware/json-template-engine.git'
          developerConnection 'scm:svn:https://github.com/vmware/json-template-engine.git'
          url 'https://github.com/vmware/json-template-engine.git'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'jyangbos'
            name 'Jim Yang'
            email 'jimyangdev@gmail.com'
          }
        }
      }
    }
  }
}

mainClassName = 'com.vmware.jsonteng.TemplateEngine'
